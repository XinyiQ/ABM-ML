;AOI is 16km*4km, here each patch reprents 26m*26m
extensions [gis csv]

breed [residents resident]
breed [tourists tourist]

turtles-own [stay-duration               ;Owned only by tourists
             activity                    ;1-working, 2-gardening, 3-picnicing, 4-hiking, 5-playing, 6-sunbathing
             prisk                       ;exposure-risk (related to activity. which also affected by tem and pre )
             exprisk                     ;exposure-risk (related to body procaution,dressing, directly influenced by temperature)
             awareness                   ;vulnerability (related to personal consciousness about "Tick" and "Tick-bite")
             bite-statu                  ;True or False
             bite-times                  ;How many times it got biten
             Frisk]                      ;Final risk

patches-own [landuse                    ;20-built-up, 60-forest, 61-sand, 62-others
             hazard                     ;Based tick abundance adding seasonal change
             tabundance                 ;Tick abundance (how many ticks in each singel patch, data from "Hazard_map.asc"), from 0 to 999
             patch-bite-count           ;How many tick-bite happened in that patch
             patch-agent-count          ;How many turtles have been in that patch
             ac1-count                  ;With activity 1, how many people got bitten in that patch
             ac2-count                  ;With activity 2, how many people got bitten in that patch
             ac3-count                  ;With activity 3, how many people got bitten in that patch
             ac4-count                  ;With activity 4, how many people got bitten in that patch
             ac5-count                  ;With activity 5, how many people got bitten in that patch
             ac6-count]                 ;With activity 6, how many people got bitten in that patch

globals [landuse-list
         landuse-dataset
         shape-dataset
         tabundance-dataset
         season                         ;2-Spring,3-Summer,4-Fall
         precipitation                  ;Daily precipitation amount in 0.1 mm over the period 08.00 preceding day - 08.00 UTC present day
         temperature                    ;Daily mean temperature in (0.1 degrees Celsius)
         whole-bite-count               ;How many tick-bite events happened
         new-bites                      ;How many tick-bite events happened in perticular tick(time)
         output-file-1                  ;Output patch-bite-count
         output-file-2                  ;Output patch-agent-count
         output-file-3                  ;Output ac1-count
         output-file-4                  ;Output ac2-count
         output-file-5                  ;Output ac3-count
         output-file-6                  ;Output ac4-count
         output-file-7                  ;Output ac5-count
         output-file-8]                 ;Output ac6-count

to setup
  clear-all
  file-close-all
  setup-environment
  setup-agents
  reset-ticks
end

to setup-agents
  create-residents 940                                            ; load residents with number of 940
    ask residents [move-to one-of patches with [landuse = 20]     ; initially ask residents stay in bulit-up area
                   set color white + 2                            ; set the color of residents black with human shape
                   set shape "person"                    
                   set awareness one-of (range 0.5 10 0.5) ]      ;set initial awareness of residents from 0.5 to 10, it can't be 0 as we use 1/awareness as agents vulnerability

  create-tourists 960                                             ; load residents with number of 940
    ask tourists [move-to one-of patches with [landuse > 0]       ; initially ask residents stay in any patch except bulit-up area
                  set color white set shape "person"              ; set the color of residents grey with human shape
                  set awareness one-of (range 0.5 10 0.5)         ; set initial awareness of residents from 0.5 to 10
                  set stay-duration (random 5 + 1)]               ; set the travel days of tourists randomly
end

to setup-environment
   set landuse-dataset gis:load-dataset "Data/Schiermonnikoog/schier_ascii.asc"        ;load the land cover data of Schiermonnikoog
   gis:set-world-envelope (gis:envelope-of landuse-dataset)
   gis:apply-raster landuse-dataset landuse
   ask patches [if landuse = 20 [set pcolor orange]               ; set built-up area color orange
                if landuse = 60 [set pcolor green]                ; set forest area color green
                if landuse = 61 [set pcolor yellow]               ; set sand area color yellow
                if landuse = 62 [set pcolor grey]]                ; set other area color grey

  set shape-dataset gis:load-dataset "Data/Schiermonnikoog/Schier_shape.shp"           ;load the boundary of Schiermonnikoog
  gis:set-drawing-color grey - 2                                                       ;set the boudary color light grey
  gis:draw shape-dataset 2                                                             ;set the Line thickness

  set tabundance-dataset gis:load-dataset "Data/hazard.ASC"                            ;load the tick abundance reference data
  gis:set-world-envelope (gis:envelope-of tabundance-dataset)
  gis:apply-raster tabundance-dataset tabundance
end

to go
     file-open "Data/TP2010.csv"                                                     ; open the Temperature and Precipitation data in 2010
    ; file-open "Data/TP2014.csv"                                                    ; open the Temperature and Precipitation data in 2014


  if file-at-end? [stop]                                                             ; define the model end when file read at end
  let weather csv:from-row file-read-line
  set season item 2 weather                                                          ; load season data, 3rd column in file (1-winter, 2-spring, 3-summer, 4-fall)
  set precipitation item 1 weather                                                   ; load precipitation data, 2nd column in file
  set temperature item 0 weather                                                     ; load temperature data, 1st column in file

  ask residents [
    ifelse ticks mod 7 != 6 and ticks mod 7 != 0                            ;define residents activity
    [set activity one-of [1 2]]                                             ;if weekdays, working or gardening
    [set activity one-of [2 3 4 5 6]]]                                      ;if weekend, gardening or picnicking or hiking or playing or sunbathing
  ask tourists [set activity one-of [3 4 5 6]]                              ;define toursit daily activity one of picnicking or hiking or playing or sunbathing

  move
  risk-set
  count-risk
  tick-bite
  count-bite-times
  adaptive-setting
  count-patch-agent
  count-patch-bite
  count-activity
  count-whole-bite

   if ticks =  269 [
   set output-file-1 gis:patch-dataset patch-bite-count
   gis:store-dataset output-file-1 "Data/OUTPUT/PBC_ascii_2010.asc"

   set output-file-2 gis:patch-dataset patch-agent-count
   gis:store-dataset output-file-2 "Data/OUTPUT/PAC_ascii_2010.asc"

    set output-file-3 gis:patch-dataset ac1-count
   gis:store-dataset output-file-3 "Data/OUTPUT/AC1_ascii_2010.asc"

    set output-file-4 gis:patch-dataset ac2-count
   gis:store-dataset output-file-4 "Data/OUTPUT/AC2_ascii_2010.asc"

    set output-file-5 gis:patch-dataset ac3-count
   gis:store-dataset output-file-5 "Data/OUTPUT/AC3_ascii_2010.asc"

    set output-file-6 gis:patch-dataset ac4-count
   gis:store-dataset output-file-6 "Data/OUTPUT/AC4_ascii_2010.asc"

    set output-file-7 gis:patch-dataset ac5-count
   gis:store-dataset output-file-7 "Data/OUTPUT/AC5_ascii_2010.asc"

    set output-file-8 gis:patch-dataset ac6-count
   gis:store-dataset output-file-8 "Data/OUTPUT/AC6_ascii_2010.asc"
  ]

  tick

  write-to-file
end

to move
  if precipitation < 76 [                         ; 7.6 millmeters defines as heavy rain
                             ask residents with [activity = 1][move-to one-of patches with [landuse = 20]]  ;ask working residents stay in built-up area
                             ask residents with [activity = 2][move-to one-of patches with [landuse = 20 or landuse = 60 or landuse = 62]]    ;ask gardening residnets stay in builtup or forest or other
                             ask turtles with [activity = 3 or activity = 4 or activity = 5][move-to one-of patches with [landuse = 60 or landuse = 61 or landuse = 62]] 
                             ask turtles with [activity = 6] [move-to one-of patches with [landuse = 61]]]   ;activity 6-sunbathing-only happens in landuse 61 "sand"
end

to risk-set
  ask turtles [if activity = 1 [set prisk 0.1]                                 
               if activity = 2 [set prisk 5.5]                               
               if activity = 3 [set prisk 4.0]                                
               if activity = 4 [set prisk 3.5]                                 
               if activity = 5 [set prisk 4.5]                             
               if activity = 6 [set prisk 5.0] ]                             

  ask turtles [if temperature < 70 [ set exprisk 0]                                    ; < 7 degrees Celsius
               if 70 <= temperature  and temperature < 140 [set exprisk 5.5]           ; > = 7 and < 14 degrees Celsius
               if 140 <= temperature and temperature < 180 [set exprisk 6.5]           ; > = 14 and < 18 degrees Celsius
               if 180 <= temperature [set exprisk 7.5]]                                ; > = 18 degrees Celsius


  ask patches [if landuse = 20 or landuse = 60 or landuse = 61 or landuse = 62
                              [                                                                        ;winter excluded
                               if season = 2 [set hazard tabundance * 0.9]                             ;2-spring
                               if season = 3 [set hazard tabundance * 1.2]                             ;3-summer
                               if season = 4 [set hazard tabundance * 0.9]]    ]                       ;4-fall
end

to count-risk
   ask turtles [set Frisk (1 / awareness) * prisk * exprisk * [hazard] of patch-here * 0.01]
end

to tick-bite                                                         ; defines how people got tick-bite
  ask turtles [ifelse Frisk > 25                                     ; here, 25 is calculated from median or average value of awareness/prisk/exprisk/hazard
                [set bite-statu "True"]
                [set bite-statu "False"]]
end

to count-bite-times                                                  ; caculate how many times a residents got tick-bite
  ask residents [ if Frisk > 25
                [set bite-times bite-times + 1]]
end

to adaptive-setting  
  ask residents [ if bite-times = 6
    [set awareness awareness * 1.1]                                  ;awareness increased 10 percent
                if bite-times = 8
    [set awareness awareness * 1.2]                                  ;awareness increased more 20 percent
                if bite-times = 10
    [set awareness awareness * 1.2]]                                 ;awareness increased more 20 percent
  ask residents [ if awareness > 15    [set awareness 15 ]]
end

to count-patch-agent
  ask patches [set patch-agent-count (patch-agent-count + count turtles-here)]                    ; records “how many turtles have been that patch”
end

to count-patch-bite
  ask patches [set patch-bite-count patch-bite-count + count turtles-here with [Frisk > 25]]         ;records “how many turtles got bitten in that patch”
end

to count-activity
  ask patches [set ac1-count ac1-count + count turtles-here with [activity = 1 and bite-statu = "True"]    ;based on the “patch-bite-count”, this records “how many times ‘working’ implemented”
               set ac2-count ac2-count + count turtles-here with [activity = 2 and bite-statu = "True"]    ;based on the “patch-bite-count”,this records “how many times ‘gardening’ implemented”
               set ac3-count ac3-count + count turtles-here with [activity = 3 and bite-statu = "True"]    ;based on the “patch-bite-count”,this records “how many times ‘picnicing’ implemented”
               set ac4-count ac4-count + count turtles-here with [activity = 4 and bite-statu = "True"]    ;based on the “patch-bite-count”,this records “how many times ‘hiking’ implemented”
               set ac5-count ac5-count + count turtles-here with [activity = 5 and bite-statu = "True"]    ;based on the “patch-bite-count”,this records “how many times ‘playing’ implemented”
               set ac6-count ac6-count + count turtles-here with [activity = 6 and bite-statu = "True"]]   ;based on the “patch-bite-count”,this records “how many times ‘sunbathing’ implemented”
end

to count-whole-bite                                                                         
  set whole-bite-count whole-bite-count + count turtles with [bite-statu = "True"]           ;record how many bitten people in a global environment  
  set new-bites count turtles with [bite-statu = "True"]                                     ;record how many bitten people in every tick (day)  
end


to write-to-file
  file-open "PBC_2010.txt"
  ask patches [file-write pxcor file-write pycor file-write patch-bite-count]
 file-print ""
 file-close

    file-open "PAC_2010.txt"
  ask patches [file-write pxcor file-write pycor file-write patch-agent-count]
 file-print ""
 file-close
end